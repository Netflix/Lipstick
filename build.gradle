import nl.javadude.gradle.plugins.license.License
import java.text.SimpleDateFormat

ext.githubProjectName = "Lipstick"

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0') {
            exclude module: 'guava'
        }
        classpath 'com.google.guava:guava:12.0'
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    repositories {
        mavenCentral()
    }
    group = 'com.netflix.lipstick'
}

apply from: file('gradle/release.gradle')

task('show-version') {
    doLast {
        println version
    }
}

apply plugin: 'license'

task(licensesAll, type: License) {
    check = false
    skipExistingHeaders = true
    source '.'
    include 'lipstick-console/src/**/*.java'
    include 'lipstick-server/app/**/*.rb'
    include 'lipstick-server/app/public/js/tossboss*.js'
    include 'lipstick-server/app/public/js/workflow*.js'
    include 'lipstick-server/app/public/js/workflow/**/*.js'
    include 'lipstick-server/app/public/tossboss*/js/*.js'
    include 'lipstick-server/app/public/*.html'
    include 'lipstick-server/app/public/tossboss*/*.html'
    include 'lipstick-server/app/public/tossboss*/css/*.css'
    include 'scripts/**'
    include 'test/integration/**'
    include 'test/unit/**'
    include 'lipstick-app/css/**'
    include 'lipstick-app/js/**'
    exclude '**/*.json'
}

license {
    header rootProject.file('codequality/HEADER')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    mapping {
        groovy='SLASHSTAR_STYLE'
        gsp='DYNASCRIPT_STYLE'
    }
}

defaultTasks 'buildRelease'

task('buildRelease', type:Copy, dependsOn: ['clean-expanded', ':lipstick-console:allJars', ':lipstick-server:war']) {
    group = "Lipstick"
    description = 'Build lipstick-server and lipstick-console and copy the artifacts to the build dir.'
    from ('./lipstick-server') {
        include '*.war'
    }
    from ('./lipstick-console/build/libs') {
        include '*.jar'
    }
    into ('build')
}

task('clean-expanded', type:Delete) {
    delete 'build'
}

project(':lipstick-server') {

  apply from: file('../gradle/maven.gradle')
  apply from: file('../gradle/convention.gradle')
  
  dependencies {
  
    runtime('org.apache.pig:pig:0.13.0') {
      exclude module: 'hsqld'
      exclude module: 'jetty'
      exclude module: 'jsp-api-2.1'
      exclude module: 'servlet-api-2.5'
    }

    runtime('org.apache.hadoop:hadoop-common:2.4.0') {
      exclude module: 'jersey-api'
      exclude module: 'jersey-json'
      exclude module: 'jersey-guice'
      exclude module: 'jersey-server'
      exclude module: 'jersey-test-framework'
    }

    runtime('org.apache.hadoop:hadoop-mapreduce-client-jobclient:2.4.0') {
      exclude module: 'jersey-api'
      exclude module: 'jersey-json'
      exclude module: 'jersey-guice'
      exclude module: 'jersey-server'
      exclude module: 'jersey-test-framework'
    }
  
    runtime('org.jruby:jruby-complete:1.7.14') {
      exclude module: 'jnr-posix'
      exclude module: 'jnr-constants'
      exclude module: 'jnr-enxio'
      exclude module: 'jnr-ffi'
      exclude module: 'jnr-netdb'
      exclude module: 'jnr-unixsocket'
      exclude module: 'jnr-x86asm'
    }

    runtime 'com.netflix.eureka:eureka-client:1.1.141'
    runtime 'org.elasticsearch:elasticsearch:1.3.2'
    runtime 'org.hibernate:hibernate-entitymanager:3.6.0.Final'
    runtime 'org.codehaus.jackson:jackson-mapper-asl:1.9.10'
  }

  task cleanLib(type: Delete) {
    delete fileTree(dir: "lib")
    delete fileTree(dir: ".gem")
    delete fileTree(dir: ".es-data")
  }
  
  task copyDepends(type: Copy) {
    from configurations.runtime
    into "lib"  
  }

  task importJar(type:Copy, dependsOn: [':lipstick-console:fullJar']) {
    from ('../lipstick-console/build/libs') {
      include '*full.jar'
    }
    into ('lib')
  }

  def gemHome = file('.gem')
  gemHome.mkdirs()

  def bundleExecutable = file('.gem/bin/bundle')
  
  task getBundler(type:JavaExec, dependsOn: ['copyDepends']) {
    onlyIf {
      !bundleExecutable.exists()
    }  
    classpath configurations.runtime
    environment('GEM_HOME', gemHome)
    args '-S', 'gem', 'install', 'bundler'
    main = "org.jruby.Main"
  }

  task gems(type:JavaExec, dependsOn: ['getBundler']) {
    environment('GEM_HOME', gemHome)
    classpath configurations.runtime
    args '-S', '.gem/bin/bundle', 'install'
    main = "org.jruby.Main"
  }

  task('run-app', type:JavaExec, dependsOn: ['gems', 'importJar'])  {
    delete fileTree(dir: ".es-data")

    environment('RUBYLIB', '.')
    environment('RACK_ENV', System.getenv('RACK_ENV') ?: 'development')
    environment('GEM_HOME', gemHome)
    classpath configurations.runtime
    args '-S', '.gem/bin/rackup'
    main = "org.jruby.Main"
  }

  task('war', type:JavaExec, dependsOn: ['gems', 'importJar'])  {
    environment('RUBYLIB', '.')
    environment('GEM_HOME', gemHome)
    classpath configurations.runtime  
    args '-S', '.gem/bin/warble'
    main = "org.jruby.Main"
  }
}

project(':lipstick-console') {
    apply plugin: 'eclipse'
    apply from: file('../gradle/convention.gradle')

    configurations {
        includeInJar
        pigInJar
        hadoopInJar
    }

    dependencies {
         includeInJar('dk.brics.automaton:automaton:1.11-8')
         includeInJar('org.codehaus.jackson:jackson-mapper-asl:1.9.10')
         includeInJar('com.google.guava:guava:12.0')
         includeInJar('org.kohsuke:graphviz-api:1.1')
         includeInJar('org.apache.commons:commons-io:1.3.2')
         includeInJar('log4j:log4j:1.2.17')
         includeInJar('joda-time:joda-time:2.1')
         includeInJar('com.googlecode.jatl:jatl:0.2.2')
         includeInJar('com.sun.jersey:jersey-client:1.8')
         includeInJar('com.sun.jersey:jersey-core:1.8')
         includeInJar('jline:jline:0.9.94')
         includeInJar('org.antlr:antlr:3.4')
         includeInJar('org.antlr:antlr-runtime:3.4')
         includeInJar('org.apache.commons:commons-math3:3.0')

         pigInJar('org.apache.pig:pig:0.12.1') {
                exclude module: 'hsqld'
                exclude module: 'jetty'
                exclude module: 'jsp-api-2.1'
                exclude module: 'servlet-api-2.5'
        }

        hadoopInJar( 'org.apache.hadoop:hadoop-core:' + (project.hasProperty('hadoopVersion') ? hadoopVersion : '1.1.0')) {
                exclude module: 'hsqld'
                exclude module: 'jetty'
                exclude module: 'jsp-api-2.1'
                exclude module: 'servlet-api-2.5'
        }

        compile 'org.hibernate:hibernate-entitymanager:3.6.0.Final'

        testCompile 'org.hsqldb:hsqldb:2.2.8'
        testCompile 'org.testng:testng:6.7'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.powermock:powermock-api-mockito:1.5'

        configurations.pigInJar.extendsFrom(configurations.includeInJar)
        configurations.hadoopInJar.extendsFrom(configurations.pigInJar)
        configurations.compile.extendsFrom(configurations.includeInJar, configurations.pigInJar, configurations.hadoopInJar)
        configurations.compile.allDependencies.all { compile it }
    }

    test {
        useTestNG()
    }

    jar {
        it.dependsOn ':clean-expanded', 'buildinfo'
        manifest { attributes 'Main-Class': 'com.netflix.lipstick.Main' }
    }

    task fullJar(type: Jar) {
        doLast { delete '../build' }
        it.dependsOn ':clean-expanded', 'buildinfo'
        classifier = "full"
        from sourceSets.main.output // that's it
        from { configurations.includeInJar.collect { it.isDirectory() ? it : zipTree(it) } }
        manifest { attributes 'Main-Class': 'com.netflix.lipstick.Main' }
    }

    task pigJar(type: Jar) {
        doLast { delete '../build' }
        it.dependsOn ':clean-expanded', 'buildinfo'
        classifier = "withPig"
        from sourceSets.main.output // that's it
        from { configurations.pigInJar.collect { it.isDirectory() ? it : zipTree(it) } }
        manifest { attributes 'Main-Class': 'com.netflix.lipstick.Main' }
    }

    task hadoopJar(type: Jar) {
        doLast { delete '../build' }
        it.dependsOn ':clean-expanded', 'buildinfo'
        classifier = "withHadoop"
        from sourceSets.main.output // that's it
        from { configurations.hadoopInJar.collect { it.isDirectory() ? it : zipTree(it) } }
        manifest { attributes 'Main-Class': 'com.netflix.lipstick.Main' }
    }

    task allJars {
        it.dependsOn 'jar', 'fullJar', 'pigJar', 'hadoopJar'
    }

    configurations.archives {
        extendsFrom configurations.sources
        extendsFrom configurations.javadoc
        extendsFrom configurations.includeInJar
        extendsFrom configurations.pigInJar
        extendsFrom configurations.hadoopInJar
    }

    artifacts {
        includeInJar fullJar
        pigInJar pigJar
        hadoopInJar hadoopJar
    }

	apply from: file('../gradle/maven.gradle')

    task('buildinfo', type:Copy) {
        group = "Lipstick"
        description = 'put the build information into lipstick-build.txt'
        from ('./buildinfo') {
            include 'lipstick_build.txt'
        }
        def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
        def ts =  df.format(new Date())
        def commitid = ''
        new ByteArrayOutputStream().withStream { os ->
            def res = exec() {
                executable = 'git'
                args = ['rev-parse','HEAD']
                ignoreExitValue = true
                standardOutput = os
            }
            commitid = os.toString()
        }

        expand(version: version, ts: ts, commitid: commitid)
        into ('./src/main/resources')
        outputs.upToDateWhen { false } 
    }
}     
